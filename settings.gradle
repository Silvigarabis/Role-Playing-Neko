pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
    }
}

buildCache {
    local {
        directory = new File(rootDir, 'build-cache')
        removeUnusedEntriesAfterDays = 30
    }
}

include(":core")
include(":configloader")

def config_loaders = [
    "snakeYamlV1"
]

config_loaders.each { loadername ->
    def name = ":configloader_" + loadername
    include(name)
    project(name).projectDir = file("configloader/loader_" + loadername)
}

def enabled_platforms = getProperty("platforms").tokenize(',').collect { it.trim() }
enabled_platforms.each { platform ->
    def name = ":platform_" + platform
    include(name)
    project(name).projectDir = file("platform/" + platform)
}

rootProject.name = "role_playing_neko"
